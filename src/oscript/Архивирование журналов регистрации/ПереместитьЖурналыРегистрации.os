#Использовать fs

#Область Переменные

// каталог из которого переносим файлы журналов регистрации
Перем КаталогХранения;
// Количество дней для анализа
Перем ПереноситьЖурналыСтаршеДней;
// каталог в который переносим файлы ЖР
Перем КаталогНазначения;
// если Архивировать, то файлы ЖР переносятся в архив
Перем Архивировать;
// текущая дата
Перем ДатаЗапускаСкрипта;
// если Истина, то оригинальные файлы удаляются после архивации
Перем УдалятьПослеАрхивации;
// имя архива по умолчанию
Перем НазваниеАрхиваПоУмолчанию;
// массив масок поиска
Перем МаскиПоиска;

#КонецОбласти

Процедура ИнициализацияПеременных()
	
	// Путь к каталогу ИБ, УИД посмотреть в файле 
	КаталогХранения = "G:/1cv8/srvinfo/reg_1541/dc415ad1-63cc-4d18-a17d-08f40f01624d/1Cv8Log";
	КаталогНазначения = "D:/Архивы Журналов";
	ПереноситьЖурналыСтаршеДней = 3;
	Архивировать = Истина;
	УдалятьПослеАрхивации = Истина;
	ДатаЗапускаСкрипта = НачалоДня(ТекущаяУниверсальнаяДата());
	НазваниеАрхиваПоУмолчанию = "journal.zip";
	
	МаскиПоиска = Новый Массив();
	МаскиПоиска.Добавить("*.lgp");
	МаскиПоиска.Добавить("*.lgx");
	
КонецПроцедуры

Процедура КопироватьЖурналРегистрации()
	Для каждого МаскаПоиска Из МаскиПоиска Цикл
		ПереместитьФайлыЖурналов(МаскаПоиска);
	КонецЦикла;
	
	Если Архивировать Тогда
		Архивировать();
	КонецЕсли;
	
КонецПроцедуры

Процедура Архивировать()
	
	НовоеОписаниеФайлов = ФайловоеОписание();
	Для каждого МаскаПоиска Из МаскиПоиска Цикл
		ФайлыЖурналов = НайтиФайлы(КаталогНазначения, МаскаПоиска);
		ЗаполнитьФайловоеОписание(ФайлыЖурналов, НовоеОписаниеФайлов);
		ФайлыЖурналов = Неопределено;
	КонецЦикла;
	
	// файлы храним в разрезе дня в одном каталоге
	Периоды = НовоеОписаниеФайлов.Скопировать( , "Дата");
	Периоды.Свернуть("Дата");
	
	Для каждого СтрокаТЧ Из Периоды Цикл
		
		ОписаниеОдногоДня = НовоеОписаниеФайлов.НайтиСтроки(Новый Структура("Дата", СтрокаТЧ.Дата));
		
		НазваниеКаталога = ФС.ОбъединитьПути(КаталогНазначения, Формат(СтрокаТЧ.Дата, "ДФ=yyyy_MM_dd"));
		
		Сообщить(СтрШаблон("Обеспечиваю каталог: %1", НазваниеКаталога));
		ФС.ОбеспечитьКаталог(НазваниеКаталога);
		
		НовоеРасположениеФайлаАрхива = ФС.ОбъединитьПути(НазваниеКаталога, НазваниеАрхиваПоУмолчанию);
		
		ЗаписьZipФайла = Новый ЗаписьZipФайла(// BSLLS:LatinAndCyrillicSymbolInWord-off
				НовоеРасположениеФайлаАрхива, , ,
				МетодСжатияZIP.Сжатие,
				УровеньСжатияZIP.Максимальный);
		
		Для каждого _Описание Из ОписаниеОдногоДня Цикл
			ЗаписьZipФайла.Добавить(_Описание.ПолноеИмяФайла);
		КонецЦикла;
		
		Сообщить(СтрШаблон("Записываю архив: %1", НовоеРасположениеФайлаАрхива));
		ЗаписьZipФайла.Записать();
		
		ЗаписьZipФайла = Неопределено; // BSLLS:LatinAndCyrillicSymbolInWord-off
		
		Если УдалятьПослеАрхивации Тогда
			Для каждого _Описание Из ОписаниеОдногоДня Цикл
				Сообщить(СтрШаблон("Удаляю файл: %1", _Описание.ПолноеИмяФайла));
				УдалитьФайлы(_Описание.ПолноеИмяФайла);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьФайловоеОписание(НайденныеФайлы, ФайловоеОписание)
	
	Для каждого ФайлЖурнала Из НайденныеФайлы Цикл
		
		НСтрока = ФайловоеОписание.Добавить();
		НСтрока.Дата = НачалоДня(ФайлЖурнала.ПолучитьВремяИзменения());
		НСтрока.ПолноеИмяФайла = ФайлЖурнала.ПолноеИмя;
		НСтрока.ИмяБезРасширения = ФайлЖурнала.ИмяБезРасширения;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ФайловоеОписание()
	
	ТаблицаОписание = Новый ТаблицаЗначений();
	ТаблицаОписание.Колонки.Добавить("Дата");
	ТаблицаОписание.Колонки.Добавить("ПолноеИмяФайла");
	ТаблицаОписание.Колонки.Добавить("ИмяБезРасширения");
	
	Возврат ТаблицаОписание;
	
КонецФункции

Процедура ПереместитьФайлыЖурналов(Знач МаскаПоиска = "*.lgp")
	
	ФайлыЖурналов = НайтиФайлы(КаталогХранения, МаскаПоиска);
	
	Для каждого НайденныйФайл Из ФайлыЖурналов Цикл
		
		ВремяИзмененияФайла = НачалоДня(НайденныйФайл.ПолучитьВремяИзменения());
		Сообщить(СтрШаблон("Время изменения файла %1 -> %2", НайденныйФайл.ПолноеИмя, ВремяИзмененияФайла));
		
		Если РазницаДней(ВремяИзмененияФайла, ДатаЗапускаСкрипта) >= ПереноситьЖурналыСтаршеДней Тогда
			
			Попытка
				
				Сообщить(СтрШаблон("Перемещаю журнал: %1", НайденныйФайл.ПолноеИмя));
				ПереместитьФайл(НайденныйФайл.ПолноеИмя, ФС.ОбъединитьПути(КаталогНазначения, НайденныйФайл.Имя));
				
			Исключение
				
				Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Продолжить;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

Функция РазницаДней(ДатаНачала, ДатаОкончания)
	
	Возврат (НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала)) / (60 * 60 * 24);
	
КонецФункции

Попытка
	
	ИнициализацияПеременных();
	КопироватьЖурналРегистрации();
	ЗавершитьРаботу(0);
	
Исключение
	
	Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	ЗавершитьРаботу(1);
	
КонецПопытки;